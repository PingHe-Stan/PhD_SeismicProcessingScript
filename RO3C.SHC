! RO3C.SHC
! 3-d rotation by computed real angles.
!
default 1 ;;        first time in s
default 2 ;;        second time in s

sdef cnt 1
sdef trmax $dsptrcs
sdef slat
sdef slon
sdef dist
sdef azim
sdef inci
sdef tazim
sdef tinci
sdef t1 #1
sdef t2 #2

settime:
if "t1 nes ;;      goto/forward t1ok:
time ;; &t1
t1ok:
if "t2 nes ;;      goto/forward t2ok:
time ;; &t2
t2ok:
if "t1 ltr "t2    goto/forward timeok:
echo >>>>>> error: reset time
calc s &t1 = ;;
calc s &t2 = ;;
goto settime:
timeok:
echo 
echo 
echo trace $hexchar09(1) r-baz $hexchar09(1) t-baz $hexchar09(1) r-inc $hexchar09(1) t-inc

nr
dtw
del h:all
loop_start:
   if "cnt gti "trmax      goto loop_exit:
!      theoretical angles
   call statloc ^station &slat &slon
   call locdiff "slat "slon ^lat ^lon &dist &tazim
   call inci "dist ^depth &tinci
   calc/fmt=<%5.1@f> r &tazim = "tazim
   calc/fmt=<%5.1@f> r &tinci = "tinci
!   calc/fmt=<%5.1@f> r &tazim = ^azimuth
!   calc/fmt=<%5.1@f> r &tinci = ^inci
   
!      real angles
   mdir 1-3 "t1 "t2 &azim &inci
   calc/fmt=<%5.1@f> r &azim = "azim
   calc/fmt=<%5.1@f> r &inci = "inci
   echo "cnt $hexchar09(1) "azim $hexchar09(1) "tazim $hexchar09(1) "inci $hexchar09(1) "tinci
   rot3 1-3 "azim "inci
   hide 1-3
!   set/file/mod 1-3 azimuth "azim
!   set/file/mod 1-3 inci "inci
   calc i &cnt = "cnt + 3
   goto loop_start:
loop_exit:
echo
rd

return


